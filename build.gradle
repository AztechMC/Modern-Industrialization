plugins {
    id 'fabric-loom' version '1.3-SNAPSHOT'
    id 'maven-publish'
    id 'com.diffplug.spotless' version '5.14.0'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
    id "com.modrinth.minotaur" version "2.+"
    id 'io.github.juuxel.loom-quiltflower' version '1.10.0'
}

apply plugin: 'java-library'

version = System.getenv("MI_VERSION")
        ? System.getenv("MI_VERSION")
        : "local"

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

repositories {
    // for KJS
    maven {
        url "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    // for Patchouli
    maven {
        url 'https://maven.blamejared.com'
    }
    // for WTHIT
    maven {
        url "https://maven2.bai.lol"
        content {
            includeGroup "lol.bai"
            includeGroup "mcp.mobius.waila"
        }
    }
    // for AE2 and JEI
    maven {
        name "Modmaven"
        url "https://modmaven.dev"
        content {
            includeGroup "appeng"
            includeGroup "mezz.jei"
        }
    }
    maven {
        url "https://maven.shedaniel.me"
        content {
            includeGroup "me.sargunvohra.mcmods"
            includeGroup "me.shedaniel.cloth"
            includeGroup "me.shedaniel"
            includeGroup "dev.architectury"
        }
    }
    maven {
        url "https://maven.terraformersmc.com"
        content {
            includeGroup "com.terraformersmc"
            includeGroup "dev.emi"
        }
    }
    maven {
        name 'Jitpack for MI'
        url 'https://jitpack.io'
        content {
            includeGroup "com.github.KubeJS-Mods"
            includeGroup "com.github.GabrielOlvH"
            /* For Magna */
            includeGroup "com.github.Draylar.omega-config"
            /* For KubeJS */
            includeGroup "com.github.llamalad7.mixinextras"
        }
    }
    // to build indrev
    maven {
        name = "CottonMC"
        url = "https://server.bbkr.space/artifactory/libs-release"
        content {
            includeGroup "io.github.cottonmc"
        }
    }
    maven {
        name = "dblsaiko"
        url = "https://maven.dblsaiko.net/"
    }
    maven {
        name = "Technici4n"
        url = "https://raw.githubusercontent.com/Technici4n/Technici4n-maven/master/"
        content {
            includeGroup "net.fabricmc.fabric-api"
            includeGroup "dev.technici4n"
            includeGroup "dev.latvian.mods"
        }
    }
    maven {
        url "https://maven.saps.dev/minecraft"
        content {
            includeGroup "dev.latvian.mods"
            includeGroup "dev.ftb.mods"
        }
    }
    // for reach entity attributes, required by Magna
    maven {
        url = "https://maven.jamieswhiteshirt.com/libs-release/"
        content {
            includeGroup "com.jamieswhiteshirt"
        }
    }
    // for PlayerAbilityLib
    maven {
        name = 'Ladysnake Mods'
        url = 'https://maven.ladysnake.org/releases'
        content {
            includeGroup 'io.github.ladysnake'
            includeGroupByRegex 'io\\.github\\.onyxstudios.*'
        }
    }
    maven {
        url "https://maven.parchmentmc.net/"
        content {
            includeGroup "org.parchmentmc.data"
        }
    }
    // for AE2
    maven {
        name = "Modmaven"
        url = uri("https://modmaven.dev/")

        content {
            includeGroup("appeng")
        }
    }
    // For the "No Indium?" mod
    maven {
        url = 'https://maven.cafeteria.dev/releases/'
        content {
            includeGroup "me.luligabi"
        }
    }
    // For FTBTeams/FTBQuests
    maven {
        url "https://maven.saps.dev/minecraft"
        content {
            includeGroup "dev.latvian.mods"
            includeGroup "dev.ftb.mods"
        }
    }
    repositories {
        maven {
            url = "https://maven.draylar.dev/releases"
            content {
                includeGroup "dev.draylar"
            }
        }
    }
    mavenLocal()

}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${project.parchment_minecraft_version}:${project.parchment_version}@zip")
    }
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    // Dependencies

    if (project.runtime_itemlist_mod == "jei") {
        modImplementation("mezz.jei:jei-${project.jei_minecraft_version}-fabric:${project.jei_version}") {
            exclude group: "mezz.jei"
        }
    } else {
        modCompileOnly("mezz.jei:jei-${project.jei_minecraft_version}-fabric:${project.jei_version}") {
            exclude group: "mezz.jei"
        }
    }

    if (project.runtime_itemlist_mod == "rei") {
        modImplementation("me.shedaniel:RoughlyEnoughItems-fabric:${project.rei_version}") {
            exclude(group: "net.fabricmc.fabric-api")
        }
    } else {
        modCompileOnly("me.shedaniel:RoughlyEnoughItems-fabric:${project.rei_version}") {
            exclude group: "net.fabricmc.fabric-api"
        }
    }

    if (project.runtime_itemlist_mod == "emi") {
        modImplementation("dev.emi:emi-fabric:${project.emi_version}+${project.emi_minecraft_version}") {
            exclude group: "net.fabricmc.fabric-api"
        }
    } else {
        modCompileOnly("dev.emi:emi-fabric:${project.emi_version}+${project.emi_minecraft_version}") {
            exclude group: "net.fabricmc.fabric-api"
        }
    }

    if (!(project.runtime_itemlist_mod in ["jei", "rei", "emi"])) {
        throw new GradleException("Invalid runtime_itemlist_mod value: " + project.runtime_itemlist_mod)
    }

    include modImplementation("vazkii.patchouli:Patchouli:${project.patchouli_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    modImplementation("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    modCompileOnly "mcp.mobius.waila:wthit-api:fabric-${project.wthit_version}"
    modRuntimeOnly "mcp.mobius.waila:wthit:fabric-${project.wthit_version}"
    modRuntimeOnly "lol.bai:badpackets:fabric-${project.badpackets_version}"

    modImplementation("com.terraformersmc:modmenu:${project.mod_menu_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    modImplementation("dev.latvian.mods:kubejs-fabric:${project.kubejs_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    include modImplementation("dev.draylar:magna:${project.magna_version}") {
        exclude(group: "io.github.prospector")
        exclude(group: "me.shedaniel.cloth")
        exclude(group: "net.fabricmc.fabric-api")
    }

    include modApi("teamreborn:energy:${project.tr_energy_version}") {
        exclude(group: "net.fabricmc.fabric-api")
        exclude group: "net.fabricmc", module: "fabric-loader"
    }

    include modImplementation("io.github.ladysnake:PlayerAbilityLib:${project.pal_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    // Include No Indium? https://github.com/Luligabi1/NoIndium
    include "me.luligabi:NoIndium:${project.no_indium_version}"

    modImplementation("appeng:appliedenergistics2-fabric:${project.ae2_version}") {
        exclude(group: "net.fabricmc.fabric-api")
        exclude(group: "me.shedaniel")
    }

    // TODO 1.20: FTB stuff
//    modCompileOnly("dev.ftb.mods:ftb-quests-fabric:${project.ftb_quests_version}") {
//        exclude(group: "net.fabricmc.fabric-api")
//        exclude(group: "me.shedaniel")
//    }
    modImplementation("dev.ftb.mods:ftb-teams-fabric:${project.ftb_teams_version}") {
        exclude(group: "net.fabricmc.fabric-api")
        exclude(group: "me.shedaniel")
    }
}

configurations {
    // due to a change in newer loom, loader isn't remapped
    // except some mods pull in an older version of loader that *is* remapped
    // so it has to be excluded explicitly.

    modRuntimeOnly.exclude group: "net.fabricmc", module: "fabric-loader"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }

    withSourcesJar()
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

sourceSets {
    main {
        java {
            srcDir "src/main"
            exclude "**/compat/dashloader/**"
        }
        resources {
            srcDir "src/generated/resources"
        }
    }
}

loom {
    splitEnvironmentSourceSets()

    accessWidenerPath = file("src/main/resources/modern_industrialization.accesswidener")
    mods {
        moderndynamics {
            sourceSet sourceSets.main
            sourceSet sourceSets.client
        }
    }
    runs {
        autoTestServer {
            server()
            name = "Autotest Server"
            vmArg "-Dmodern_industrialization.autoTest"

            ideConfigGenerated = true
        }
        datagenClient {
            client()
            name "Data Generation"
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.modid=modern_industrialization"
            vmArg "-Dfabric-api.datagen.output-dir=${file("src/generated/resources")}"
            vmArg "-Dfabric-api.datagen.strict-validation=true"

            ideConfigGenerated = true
            runDir "build/datagen"
            source sourceSets.client
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"

    // Minecraft 1.17 (21w19a) upwards uses Java 16.
    it.options.release = 17
}

jar {
    from "LICENSE"
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    // select the repositories you want to publish to
    repositories {
        // uncomment to publish to the local maven
        // mavenLocal()
    }
}

spotless {
    format 'misc', {
        target '*.gradle', '*.md', '.gitignore'

        trimTrailingWhitespace()
        indentWithSpaces()
        endWithNewline()
    }
    java {
        importOrder()

        removeUnusedImports()

        eclipse().configFile('eclipse-format.xml')

        indentWithSpaces()
        endWithNewline()
        licenseHeaderFile 'LICENSE.java'
    }
    format 'json', {
        target 'src/main/**/lang/*.json'
        addStep(JsonOrderStep.create())
    }
}

import com.diffplug.spotless.FormatterFunc
import com.diffplug.spotless.FormatterStep
import com.google.gson.Gson
import com.google.gson.GsonBuilder

final class JsonOrderStep {
    private JsonOrderStep() {}
    private static final Gson GSON = new GsonBuilder().disableHtmlEscaping().setLenient().setPrettyPrinting().create()

    static FormatterStep create() {
        return FormatterStep.create(
                'jsonOrderStep',
                new State(),
                {
                    new FormatterFunc() {
                        String apply(String input) {
                            TreeMap<String, Object> jsonMap = GSON.fromJson(input, TreeMap.class)
                            String sortedJson = GSON.toJson(jsonMap)
                            String prettyPrinted = sortedJson.replace('\\u0027', '\'')
                            return prettyPrinted + "\n"
                        }
                    }
                },
        )
    }

    private static final class State implements Serializable {
        private static final long serialVersionUID = 1L

        private final int formatVersion = 1
    }
}

def releaseChannel = "release"
def changelog = "Please visit our github repository for a changelog: https://github.com/AztechMC/Modern-Industrialization/releases."
if (version.toLowerCase().contains("alpha")) {
    releaseChannel = "alpha"
    changelog = "THIS IS AN ALPHA RELEASE, MAKE A BACKUP BEFORE INSTALLING AND FREQUENTLY WHILE PLAYING, AND PLEASE REPORT ANY ISSUE YOU MAY FIND ON OUR ISSUE TRACKER.\n\n" + changelog
} else if (version.toLowerCase().contains("beta")) {
    releaseChannel = "beta"
    changelog = "This is a beta release. It is expected to be mostly stable, but in any case please report any issue you may find.\n\n" + changelog
}

// Publishing
if (System.getenv("CURSEFORGE_API_KEY") && !version.endsWith("-SNAPSHOT")) {
    curseforge {
        apiKey = System.getenv().get("CURSEFORGE_API_KEY")
        project {
            id = project.curseforge_project
            releaseType = releaseChannel
            changelogType = "markdown"
            delegate.changelog = changelog

            addGameVersion "1.20.1"
            addGameVersion "Fabric"
            addGameVersion "Java 17"

            relations {
                requiredDependency "fabric-api"
                requiredDependency "cloth-config"
                optionalDependency "wthit"
                optionalDependency "emi"
                optionalDependency "jei"
                optionalDependency "roughly-enough-items"
                embeddedLibrary "no-indium"
                embeddedLibrary "patchouli-fabric"
                embeddedLibrary "pal"
            }
            mainArtifact(remapJar.archiveFile) {
                displayName = "Modern Industrialization v${version}"
            }
        }
        options {
            forgeGradleIntegration = false
        }
        afterEvaluate {
            tasks.getByName("curseforge${project.curseforge_project}").dependsOn remapJar
        }
    }
}


modrinth {
    token = System.getenv("MODRINTH_API_KEY")
    projectId = project.modrinth_project
    versionNumber = version
    versionName = "Modern Industrialization v${version}"
    delegate.changelog = changelog
    versionType = releaseChannel
    uploadFile = remapJar
    gameVersions = ["1.20.1"]
    loaders = ["fabric"]
    dependencies {
        required.project "fabric-api"
        required.project "cloth-config"
        optional.project "wthit"
        optional.project "emi"
        optional.project "jei"
        optional.project "rei"
        embedded.project "no-indium"
        embedded.project "patchouli"
        // add "pal" (PlayerAbilityLib) when its on modrinth
    }
}
tasks.modrinth.onlyIf {
    System.getenv("MODRINTH_API_KEY") && !version.endsWith("-SNAPSHOT")
}
